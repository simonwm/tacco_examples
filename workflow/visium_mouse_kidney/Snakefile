localrules: download_TabulaMuris_count_data, download_MouseKidney_visium_data, download_MouseKidneyCoronal_v110_visium_data

rule visium_mouse_kidney_run_notebook:
    input:
        reference="results/visium_mouse_kidney/data/TabulaMurisKidney.h5ad",
        slide_1="results/visium_mouse_kidney/data/MouseKidney_visium.h5ad",
        slide_2="results/visium_mouse_kidney/data/MouseKidneyCoronal_v110_visium.h5ad",
    output:
        "notebooks/visium_mouse_kidney.ipynb"
    conda:
        "../envs/TACCO_notebook_env.yml"
    resources:
        mem_mb=8000
    log:
        "logs/visium_mouse_kidney/run_notebook.log"
    benchmark:
        "benchmarks/visium_mouse_kidney/run_notebook.tsv"
    shell:
        "jupyter nbconvert --to notebook --execute workflow/visium_mouse_kidney/notebook.ipynb --output ../../{output}" 

rule download_TabulaMuris_count_data:
    output:
        droplet_zip = temp("resources/visium_mouse_kidney/TabulaMuris_count_data/droplet.zip"),
        metadata = temp("resources/visium_mouse_kidney/TabulaMuris_count_data/annotations_droplets.csv"),
    log:
        "logs/visium_mouse_kidney/TabulaMuris/download_TabulaMuris_count_data.log"
    shell:
        '(mkdir -p resources/visium_mouse_kidney/TabulaMuris_count_data/ && '
        'pushd resources/visium_mouse_kidney/TabulaMuris_count_data/ && '
        'wget -O TabulaMuris_count_data.zip https://figshare.com/ndownloader/articles/5715025/versions/1 && '
        'unzip TabulaMuris_count_data.zip) &> {log}'

rule download_MouseKidney_visium_data:
    output:
        h5 = temp("resources/visium_mouse_kidney/MouseKidney_visium_data/filtered_feature_bc_matrix.h5"),
        visium_dir = temp(directory("resources/visium_mouse_kidney/MouseKidney_visium_data")),
    log:
        "logs/visium_mouse_kidney/MouseKidney_visium/download_MouseKidney_visium_data.log"
    shell:
        '(mkdir -p resources/visium_mouse_kidney/MouseKidney_visium_data/ && '
        'pushd resources/visium_mouse_kidney/MouseKidney_visium_data/ && '
        'wget -O filtered_feature_bc_matrix.h5 https://cf.10xgenomics.com/samples/spatial-exp/1.3.0/Visium_FFPE_Mouse_Kidney/Visium_FFPE_Mouse_Kidney_filtered_feature_bc_matrix.h5 && '
        'wget https://cf.10xgenomics.com/samples/spatial-exp/1.3.0/Visium_FFPE_Mouse_Kidney/Visium_FFPE_Mouse_Kidney_spatial.tar.gz && '
        'tar -xzf Visium_FFPE_Mouse_Kidney_spatial.tar.gz) &> {log}'
        
rule download_MouseKidneyCoronal_v110_visium_data:
    output:
        h5 = temp("resources/visium_mouse_kidney/MouseKidneyCoronal_v110_visium_data/filtered_feature_bc_matrix.h5"),
        visium_dir = temp(directory("resources/visium_mouse_kidney/MouseKidneyCoronal_v110_visium_data")),
    log:
        "logs/visium_mouse_kidney/MouseKidneyCoronal_visium/download_MouseKidneyCoronal_v110_visium_data.log"
    shell:
        '(mkdir -p resources/visium_mouse_kidney/MouseKidneyCoronal_v110_visium_data/ && '
        'pushd resources/visium_mouse_kidney/MouseKidneyCoronal_v110_visium_data/ && '
        'wget -O filtered_feature_bc_matrix.h5 https://cf.10xgenomics.com/samples/spatial-exp/1.1.0/V1_Mouse_Kidney/V1_Mouse_Kidney_filtered_feature_bc_matrix.h5 && '
        'wget https://cf.10xgenomics.com/samples/spatial-exp/1.1.0/V1_Mouse_Kidney/V1_Mouse_Kidney_spatial.tar.gz && '
        'tar -xzf V1_Mouse_Kidney_spatial.tar.gz) &> {log}'

rule prepTabulaMuris_count_data:
    input:
        droplet_zip="resources/visium_mouse_kidney/TabulaMuris_count_data/droplet.zip",
        metadata="resources/visium_mouse_kidney/TabulaMuris_count_data/annotations_droplets.csv"
    output:
        scrnaseq_h5ad="results/visium_mouse_kidney/data/TabulaMurisKidney.h5ad",
    conda:
        "../envs/TACCO_notebook_env.yml"
    resources:
        mem_mb=8000
    log:
        "logs/visium_mouse_kidney/prepTabulaMuris_count_data.log"
    benchmark:
        "benchmarks/visium_mouse_kidney/prepTabulaMuris_count_data.tsv"
    script:
        "prepTabulaMuris_count_data.py"

rule prepVisium_mouse_spatial_data:
    input:
        visium_dir_ffpe=rules.download_MouseKidney_visium_data.output.visium_dir,
        h5_ffpe=rules.download_MouseKidney_visium_data.output.h5,
        visium_dir_coronal=rules.download_MouseKidneyCoronal_v110_visium_data.output.visium_dir,
        h5_coronal=rules.download_MouseKidneyCoronal_v110_visium_data.output.h5,
    output:
        visium_ffpe_h5ad="results/visium_mouse_kidney/data/MouseKidney_visium.h5ad",
        visium_coronal_h5ad="results/visium_mouse_kidney/data/MouseKidneyCoronal_v110_visium.h5ad",
    conda:
        "../envs/TACCO_notebook_env.yml"
    resources:
        mem_mb=8000
    log:
        "logs/visium_mouse_kidney/prepVisium_mouse_spatial_data.log"
    benchmark:
        "benchmarks/visium_mouse_kidney/prepVisium_mouse_spatial_data.tsv"
    script:
        "prepVisium_mouse_spatial_data.py"
